apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.3'
group 'com.aimmatic.natural'

task sourceJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/voice-android-release.aar")
            artifact(sourceJar)
            artifact(javadocJar)
            groupId this.group
            artifactId 'voice-android'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('BINTRAY_API_KEY') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'natural-voice'
        name = 'com.aimmatic.natural'
        description = "AimMatic Natural Voice Mobile SDK."
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/aimmatic/natural-voice-mobile-sdk-android.git'
        publish = true
        dryRun = false
        version {
            name = this.version
            desc = "Natural Voice Mobile SDK ${this.version}"
            released = new Date()
            vcsTag = this.version
            gpg {
                sign = true
                passphrase = project.hasProperty('BINTRAY_GPG_PASSWORD') ?: System.getenv('BINTRAY_GPG_PASSWORD')
            }
        }
    }
}